#region

//function Transitions(){
global.midTransition = false;
global.roomTarget = -1;

//Places the sequences in the room 
function TransitionPlaceSequence(_type){
	if (layer_exists("transition")){
		layer_destroy("transition");
	}
	var _lay = layer_create(-9999, "transition");
	layer_sequence_create(_lay, 0, 0, _type);
}

//Called whenever you want to go from one room to another, using any combination of in/out sequences
function TransitionStart(_roomTarget, _typeOut, _typeIn){
	if(!global.midTransition){
		global.midTransition = true;
		global.roomTarget = _roomTarget;

		//Destroy oMouse and reset cursor before transitioning
		if(instance_exists(oMouse)){
			cursor_sprite = -1;
			window_set_cursor(cr_none);
			instance_destroy(oMouse);
        }
		
		TransitionPlaceSequence(_typeOut);
		layer_set_target_room(_roomTarget);
		TransitionPlaceSequence(_typeIn);
		layer_reset_target_room();
		return true;
	}
	else return false;
}

//Called as a moment at the end of an "Out" transition sequence
function TransitionChangeRoom(){
	room_goto(global.roomTarget);
}

//Called as a moment at the end of an "In" transition sequence
function TransitionFinished(){
	layer_sequence_destroy(self.elementID);
	global.midTransition = false;
}
#endregion